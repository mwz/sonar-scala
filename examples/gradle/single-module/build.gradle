buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.scoverage:gradle-scoverage:2.5.0"
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.22"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

apply plugin: 'scala'
apply plugin: 'org.scoverage'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.maiflai.scalatest'

repositories {
    mavenCentral()
}

// Example global configuration settings.
sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/scala"
        property "sonar.tests", "src/test/scala"
        property "sonar.junit.reportPaths", "build/test-results/test"
        property "sonar.scala.version", "2.12"
        property "sonar.scala.scoverage.reportPath", "build/reports/scoverage/scoverage.xml"
        property "sonar.scala.scapegoat.reportPath", "build/reports/scapegoat/scapegoat.xml"
    }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.12.8'
    scoverage 'org.scoverage:scalac-scoverage-plugin_2.12:1.3.1',
            'org.scoverage:scalac-scoverage-runtime_2.12:1.3.1'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
    testRuntime 'org.pegdown:pegdown:1.4.2'
}


// Scapegoat configuration.
configurations {
  scalaCompilerPlugin
}

dependencies {
  compile 'com.sksamuel.scapegoat:scalac-scapegoat-plugin_2.12:1.3.3' // to prevent https://github.com/sksamuel/scapegoat/issues/98
  scalaCompilerPlugin "com.sksamuel.scapegoat:scalac-scapegoat-plugin_2.12:1.3.3"
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.additionalParameters = [
    "-Xplugin:" + configurations.scalaCompilerPlugin.asPath,
    "-P:scapegoat:dataDir:" + buildDir + "/reports/scapegoat",
    "-P:scapegoat:reports:all"
  ]
}
